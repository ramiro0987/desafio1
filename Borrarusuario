pipeline {
    agent any
    parameters {
        string(name: 'USER_UID', defaultValue: '', description: 'UID del usuario Linux a eliminar (sin el UID no se procederá): ')
    }
    stages {
        stage('Mostrar usuarios antes de eliminar') {
            steps {
                script {
                    echo "Usuarios en el sistema antes de la eliminación:"
                    sh 'awk -F: \'$3 >= 1000 && $3 < 60000 {print $1}\' /etc/passwd'
                }
            }
        }
        
        stage('Eliminar usuario') {
            steps {
                script {
                    if (params.USER_UID) {
                        // Obtener información del usuario basado en el UID:
                        def userInfo = sh(script: "getent passwd ${params.USER_UID}", returnStdout: true).trim()
                        def username = userInfo.split(':')[0]
                        def primaryGroupID = userInfo.split(':')[3]
                        def primaryGroupName = sh(script: "getent group ${primaryGroupID} | cut -d: -f1", returnStdout: true).trim()
                        
                        // Comprobación si el usuario está en el grupo 'sudo' y eliminarlo de ser necesario:
                        def isInSudoGroup = sh(script: "getent group sudo | grep -w ${username}", returnStatus: true) == 0
                        if (isInSudoGroup) {
                            sh "sudo deluser ${username} sudo"
                            echo "Usuario ${username} removido del grupo sudo."
                        } else {
                            echo "El usuario ${username} no está en el grupo sudo."
                        }

                        // Eliminar usuario y su directorio HOME, manejar excepciones:
                        try {
                            sh "sudo userdel -r ${username}"
                            echo "Usuario ${username} eliminado correctamente."
                        } catch (Exception e) {
                            echo "Error al eliminar usuario ${username}: ${e.message}"
                        }

                        // Eliminar el grupo primario si existe:
                        try {
                            sh "sudo groupdel ${primaryGroupName}"
                            echo "Grupo ${primaryGroupName} eliminado correctamente."
                        } catch (Exception e) {
                            echo "No se pudo eliminar el grupo ${primaryGroupName}: ${e.message}"
                        }
                    } else {
                        echo "UID no proporcionado. No se procederá con la eliminación."
                    }
                }
            }
        }

        stage('Mostrar usuarios después de la eliminación') {
            steps {
                script {
                    echo "Usuarios en el sistema después de la eliminación:"
                    sh 'awk -F: \'$3 >= 1000 && $3 < 60000 {print $1}\' /etc/passwd'
                }
            }
        }
    }

    post {
        always {
            echo 'Proceso de eliminación finalizado.'
        }
    }
}

